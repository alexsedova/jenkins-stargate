plugins {
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

group 'com.swedbank'
version project.hasProperty('version') ? project.property('version') : 'unspecified'
String vcsUrl = 'https://github.com/swedbank/jenkins-stargate.git'

apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile 'com.lesfurets:jenkins-pipeline-unit:1.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.12.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.0'

    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile ('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            artifact jar

            groupId group
            artifactId rootProject.name
            version version

            pom {
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'obaranov'
                        name = 'Oleksii Baranov'
                        email = 'aleksey.baranov@gmail.com'
                    }
                }
                scm {
                    url = vcsUrl
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    configurations.compile.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenPublication']
    pkg {
        repo = group
        name = rootProject.name
        userOrg = 'swedbank-test-org'
        licenses = ['Apache-2.0']
        vcsUrl = vcsUrl
        version {
            released  = new Date()
        }
    }
}

